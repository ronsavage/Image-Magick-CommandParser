:default				::= action => ::first

#lexeme default			= latm => 1		# Longest Acceptable Token Match.

# G1-level rules in top-down order.

:start					::= command_and_options

command_and_options		::= command_name input_file_name rule_set

command_name			::= 'convert'						action => command
							| 'mogrify'						action => command

input_file_name			::= string			rank => 1		action => input_file
input_file_name			::=					rank => 0		action => input_file

rule_set				::= rule*

rule					::= close_parenthesis	rank => 1
							| open_parenthesis	rank => 1
							| operator_name		rank => 1
							| action_set		rank => 0

action_set				::= sign action_name parameter_set	action => action_set

sign					::= minus_sign
							| plus_sign

action_name				::= string

parameter_set			::= string*

operator_name			::= operator_prefix ':' operator_suffix	action => operator_name

operator_prefix			::= string

operator_suffix			::= string*

close_parenthesis		::= close_paren						action => close_parenthesis

open_parenthesis		::= open_paren						action => open_parenthesis

# L0-level rules (lexemes) in alphabetical order.

close_paren				~ ')'

open_paren				~ '('

minus_sign				~ '-'

plus_sign				~ '+'

string					~ string_char+
string_char				~ [^-+\(\)\s]

# L0 lexemes for the boilerplate.

:discard				~ whitespace
whitespace				~ [\s]+

