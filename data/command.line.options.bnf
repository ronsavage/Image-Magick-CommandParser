:default				::= action => [values]

#lexeme default			= latm => 1		# Longest Acceptable Token Match.

# G1 rules in top-down order.

:start					::= command_and_options

command_and_options		::= command_name input_file_name rule_set

command_name			::= 'convert'						action => command
							| 'mogrify'						action => command

input_file_name			::= string			rank => 1		action => input_file
input_file_name			::=					rank => 0		action => input_file

rule_set				::= rule*

rule					::= action_set
							| close_parenthesis				action => close_parenthesis
							| open_parenthesis				action => open_parenthesis

action_set				::= sign action_name parameter_set	action => action_set
							| operator						action => operator

action_name				::= string

operator				::= operator_name

parameter_set			::= string*

# L0 rules (lexemes) in alphabetical order.

operator_name			~ operator_name_char+
operator_name_char		~ [^-+\(\)\s0-9]

close_parenthesis		~ ')'

open_parenthesis		~ '('

sign					~ [-+]

string					~ string_char+
string_char				~ [^-+\(\)\s]

# L0 rules (lexemes) for the boilerplate.

:discard				~ whitespace
whitespace				~ [\s]+

